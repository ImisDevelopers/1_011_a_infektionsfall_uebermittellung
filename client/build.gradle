plugins {
  id "com.github.node-gradle.node" version "2.2.4"
}

apply plugin: 'base'
version '0.0.1'

node {
  version = '14.3.0'
  download = true
}

def sourceSet = fileTree(dir: '.', includes: [
		'src',
		'package.json',
		'babel.config.js',
		'tsconfig.json',
		'vue.config.js',
		'yarn.lock',
		'node_modules'
])

task buildFrontend(type: YarnTask) {
  inputs.files sourceSet
  outputs.dir 'dist'
  args = ['build']
}

task lint(type: YarnTask) {
  inputs.files sourceSet
  outputs.files sourceSet
  args = ['run', 'lint']
}

task test(type: YarnTask) {
  args = ['run', 'test:unit']
}

task serve(type: YarnTask) {
  args = ['serve']
}

task generateClient(type: NpxTask) {
  inputs.file('../server/build/resources/swagger.json')
  outputs.file('src/api/SwaggerApi.ts')

  command = 'swagger-typescript-api'
  args = ['-p', '../server/build/resources/swagger.json', "-o", "./src/api", "-n", "SwaggerApi.ts"]
}

tasks.assemble.dependsOn('buildFrontend')
tasks.test.dependsOn('assemble')
tasks.serve.dependsOn('generateClient')
tasks.lint.dependsOn('yarn_install', 'generateClient')
tasks.buildFrontend.dependsOn('lint')
tasks.buildFrontend.dependsOn('yarn_install')