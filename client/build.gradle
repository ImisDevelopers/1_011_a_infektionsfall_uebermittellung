plugins {
  id "com.github.node-gradle.node" version "2.2.4"
}

apply plugin: 'base'
version '0.0.1'

node {
  version = '14.3.0'
  download = true
}

def sourceSet = fileTree(dir: '.', includes: [
		'src/**',
		'package.json',
		'babel.config.js',
		'tsconfig.json',
		'vue.config.js',
		'yarn.lock',
		'node_modules/**'
])

task buildFrontend(type: YarnTask) {
  setDescription "Build minified distribution files for the frontend code."
  inputs.files sourceSet
  outputs.dir 'dist'
  args = ['build']
}

task lint(type: YarnTask) {
  setDescription "Run eslint on the frontend code."
  inputs.files sourceSet
  args = ['run', 'lint']
}

task test(type: YarnTask) {
  setDescription "Run frontend tests."
  args = ['run', 'test:unit']
}

task serve(type: YarnTask) {
  setDescription "Run a local frontend development server."
  args = ['serve']
}

task generateClient(type: NpxTask) {
  def swaggerConfigPath = "../server/build/resources/swagger.json"
  setDescription "Generate Swagger API client from Swagger config at ${swaggerConfigPath}."
  inputs.file(swaggerConfigPath)
  outputs.file('src/api/SwaggerApi.ts')

  command = 'swagger-typescript-api'
  args = ['-p', swaggerConfigPath, "-o", "./src/api", "-n", "SwaggerApi.ts"]
}

tasks.assemble.dependsOn('lint', 'buildFrontend')
tasks.test.dependsOn('assemble')
tasks.lint.dependsOn('yarn_install', 'generateClient')
tasks.buildFrontend.dependsOn('yarn_install')
tasks.buildFrontend.mustRunAfter('lint')
